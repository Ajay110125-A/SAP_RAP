CLASS zcl_modify_practice_ay DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_oo_adt_classrun .
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_modify_practice_ay IMPLEMENTATION.


  METHOD if_oo_adt_classrun~main.

*MODIFY ENTITY, ENTITIES, field_spec
*1->...  { FROM fields_tab }
*       CREATE, CREATE BY, UPâ˜ºDATE, DELETE, EXECUTE
*       For DELETE, EXECUTE we can use this option only
*       The %control structure must be filled explicitly in the internal table fields_tab for CREATE, CREATE BY and UPDATE
    DATA : lt_booking TYPE TABLE FOR CREATE zi_travel_ay_m\_Booking,
           lt_booksup TYPE TABLE FOR CREATE zi_booking_ay_m\_BookingSuppl.
**********************************************************************
*Using 1st Entity Forms
    MODIFY ENTITY zi_travel_ay_m
        CREATE FROM VALUE #(
                            (
                                %cid = 'cid1'
                                %data-BeginDate = '20240623'
                                %control-BeginDate = if_abap_behv=>mk-on
                            )
                           )
        CREATE BY \_Booking
            FROM VALUE #(
                          (
                            %cid_ref = 'cid1'
                            %target = VALUE #( (
                                                %cid = 'cid11'
                                                %data-bookingdate = '20240623'
                                                %control-bookingdate = if_abap_behv=>mk-on
                                               )
                                             )
                          )
                        )


        FAILED FINAL(lt_failed)
        REPORTED FINAL(lt_reported)
        MAPPED  FINAL(lt_mapped).
**********************************************************************
*    Creating a DATA Using MODIFY along with it's child entity
**********************************************************************
*USing 2nd Entity Forms
    MODIFY ENTITIES OF zi_travel_ay_m
        ENTITY zi_travel_ay_m
       CREATE FROM VALUE #(
                            (
                                %cid = 'cid1' "Here we have to give random CID because we are creating without Key fields which is used as reference for
                                              "it's child entity and this CID is assigned by the RAP at real time FIORI APP Scenario
                                %data-BeginDate = '20240623'
                                %control-BeginDate = if_abap_behv=>mk-on
                            )
                           )
        CREATE BY \_Booking
        FROM VALUE #(
                          (
                            %cid_ref = 'cid1'
                            %target = VALUE #( (
                                                %cid = 'cid11'
                                                %data-bookingdate = '20240623'
                                                %control-bookingdate = if_abap_behv=>mk-on
                                               )
                                             )
                          )
                        )
        ENTITY zi_booking_ay_m
        CREATE BY \_BookingSuppl
        FROM VALUE #(
                     (
                        %cid_ref = 'cid11'
                        %target = VALUE #(
                                            (
                                                %cid = 'cid111'
                                                %data-supplimentid = 'P1'
                                                %control-supplimentid = if_abap_behv=>mk-on
                                            )
                                         )
                     )
                    )
       FAILED FINAL(ls_failed)
        REPORTED FINAL(ls_reported)
        MAPPED  FINAL(ls_mapped).

    IF ls_failed IS NOT INITIAL.
      out->write( ls_failed ).
    ELSE.
      COMMIT ENTITIES.
    ENDIF.

    MODIFY ENTITIES OF zi_travel_ay_m
        ENTITY zi_travel_ay_m
          DELETE FROM VALUE #(
                               ( %key-TravelId = '00004269' )
                               ( %key-TravelId = '00004270' )
                               ( %key-TravelId = '00004271' )
                               ( %key-TravelId = '00004272' )
                             )
    FAILED FINAL(ls_delete_failed)
    REPORTED FINAL(ls_delete_reported)
    MAPPED FINAL(ls_delete_mapped).

    IF ls_delete_failed IS NOT INITIAL.
      out->write( ls_delete_failed ).
    ELSE.
      COMMIT ENTITIES.
    ENDIF.

**********************************************************************
*2->   | { AUTO FILL CID WITH fields_tab }
**********************************************************************
* Here we cannot use CID AUTO FILL will automatically fills it and because we filling automatically and we cannot create child entities due to
*we don't know CID value for reference purposes
    MODIFY ENTITY zi_travel_ay_m
           CREATE AUTO FILL CID WITH VALUE #(
                               (
                                    %data-BeginDate = '20240629'
                                    %control-BeginDate = if_abap_behv=>mk-on
                               )
                              )
           FAILED FINAL(ls_create_failed)
           REPORTED FINAL(ls_create_repoted)
           MAPPED FINAL(ls_create_mapped).
    IF ls_create_failed IS NOT INITIAL.
      out->write( ls_create_failed ).
    ELSE.
      COMMIT ENTITIES.
    ENDIF.
**********************************************************************
*3->   | { [AUTO FILL CID] FIELDS ( comp1 comp2 ... ) WITH fields_tab }
**********************************************************************
    MODIFY ENTITIES OF zi_travel_ay_m
        ENTITY zi_travel_ay_m
        UPDATE FIELDS ( BeginDate ) "Here no need to display control structure because here we are already begin date for updation.
        WITH VALUE #( (  %key-TravelId = '00004274'
                         %data-BeginDate = '20240622' ) ) "This is recommended for updation of fields

        ENTITY zi_travel_ay_m
        DELETE FROM VALUE #( ( %key-TravelId = '00004572' ) )
        .

    COMMIT ENTITIES.
**********************************************************************
*4->  | { [AUTO FILL CID] SET FIELDS WITH fields_tab }
**********************************************************************
    MODIFY ENTITY zi_travel_ay_m "In this from no need of control structure and FILEDS key, it will automatically updates %data values given at code
        UPDATE SET FIELDS WITH VALUE #( (
                                           %key-TravelId = '00004274'
                                           %data-BeginDate = '20240623'
                                      ) ). "This is not recommended as many no. of data leads to performance issues
    COMMIT ENTITIES.























  ENDMETHOD.
ENDCLASS.